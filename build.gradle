plugins {
    id 'java'
	id 'fabric-loom' version '0.10-SNAPSHOT'
}

sourceCompatibility = JavaVersion.toVersion(project.java_version)
targetCompatibility = JavaVersion.toVersion(project.java_version)

archivesBaseName = project.mod_id
group = "com.kale_ko"
version = project.mod_version

repositories {
    mavenCentral()
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://maven.shedaniel.me/" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.minecraft_version}+${project.yarn_mappings}"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}+${project.mod_compatibility}"

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)

    options.encoding = "UTF-8"
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "mod_id": project.mod_id,
        "mod_version": project.mod_version,
        "java_version": project.java_version,
        "minecraft_version": project.minecraft_version,
        "mod_compatibility": project.mod_compatibility,
        "fabric_loader_version": project.fabric_loader_version,
        "fabric_api_version": project.fabric_api_version,
        "modmenu_version": project.modmenu_version,
        "cloth_config_version": project.cloth_config_version
    }

    filesMatching("better_vanilla.mixins.json") {
        expand "mod_id": project.mod_id,
        "java_version": project.java_version
    }
}

jar {
	archiveFileName = project.archivesBaseName + "-" + project.version + ".jar"
    destinationDirectory = file("run\\mods")
}

java {
	withSourcesJar()
}

tasks.register("runDevClient", JavaExec) {
    main = "net.fabricmc.devlaunchinjector.Main"
    classpath = sourceSets.main.runtimeClasspath
    workingDir = file("run\\")
    jvmArgs "-Dfabric.dli.config\u003d${projectDir.path}\\.gradle\\loom-cache\\launch.cfg", "-Dfabric.dli.env\u003dclient", "-Dfabric.dli.main\u003dnet.fabricmc.loader.impl.launch.knot.KnotClient"
}

tasks.register("runDevServer", JavaExec) {
    main = "net.fabricmc.devlaunchinjector.Main"
    classpath = sourceSets.main.runtimeClasspath
    workingDir = file("run\\")
    jvmArgs "-Dfabric.dli.config\u003d${projectDir.path}\\.gradle\\loom-cache\\launch.cfg", "-Dfabric.dli.env\u003dserver", "-Dfabric.dli.main\u003dnet.fabricmc.loader.impl.launch.knot.KnotServer"
    args "nogui"
}

tasks.register("buildAndRun") {
    dependsOn tasks.jar
    dependsOn tasks.runDevClient
}

tasks.register("buildAndRunServer") {
    dependsOn tasks.jar
    dependsOn tasks.runDevServer
}